plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
}

group 'com.github.unq-arqsoft-difi'
version '1.0.0-rc1'

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    // Kotlin & friends
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.72"
    implementation "io.javalin:javalin:3.8.0"
    implementation "org.slf4j:slf4j-simple:1.7.28"
    implementation 'io.github.cdimascio:java-dotenv:5.1.4'

    // Database
    implementation "org.jetbrains.exposed:exposed-core:0.24.1"
    implementation "org.jetbrains.exposed:exposed-dao:0.24.1"
    implementation "org.jetbrains.exposed:exposed-jdbc:0.24.1"
    implementation "org.xerial:sqlite-jdbc:3.30.1"

    // Jackson
    implementation "com.fasterxml.jackson.core:jackson-databind:2.11.0"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.11.0"

    // Junit Jupiter
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.2"

    // Fuel (http server)
    testImplementation "com.github.kittinunf.fuel:fuel:2.2.1"
    testImplementation "com.github.kittinunf.fuel:fuel-jackson:2.2.1"

    // Linter
    ktlint "com.pinterest:ktlint:0.35.0"
}

task run(type: JavaExec) {
    main = 'difi.covid.api.Main'
    classpath = sourceSets.main.runtimeClasspath
}

test {
    useJUnitPlatform()
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

coveralls {
    sourceDirs += ['src/main/kotlin']
}
