buildscript {
    ext.kotlin_version = '1.3.72'
    ext.junit_version = '5.6.2'
    ext.spek_version = '2.0.6'
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
}

configurations {
    ktlint
}

group 'com.github.unq-arqsoft-difi'
version '1.0.0-rc1'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Junit Jupiter
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"

    // Spek Test Framework
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spek_version"
    testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version" // spek requires kotlin-reflect

    ktlint "com.pinterest:ktlint:0.35.0"
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

coveralls {
    sourceDirs += ['src/main/kotlin']
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
